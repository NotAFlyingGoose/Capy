
// todo: I want to do smth more like zig's `allocPrint` and `bufPrint`
// https://github.com/ziglang/zig/blob/905c85be968e6823305887474d2a821dac82f16c/lib/std/fmt.zig#L1436

core :: mod "core";
ptr :: core.ptr;
math :: core.math;
string_builder :: core.string_builder;

StringBuilder :: string_builder.StringBuilder;

i64_to_string :: (n: i64) -> StringBuilder {
    // idk man i'm just copying rust
    str := string_builder.make_with_capacity(39);
    
    new_len := _write_i64_to_buf(n, true, (^mut u8).((^mut any).(str.buf)), 0, 0);

    str.len = new_len;

    string_builder.shrink_to_fit(^mut str);

    str
}

u64_to_string :: (n: u64) -> StringBuilder {
    n := n;
    digits := core.math.lg_u64(n) + 1;
    str := string_builder.make_with_capacity(digits);
    str.len = digits;

    while digits > 0 {
        remainder :: n % 10;
        n         =  n / 10;
        digits    =  digits - 1;

        ptr.write(
            str.buf,
            u8.('0') + u8.(remainder),
            digits
        );
    }

    str
}

f64_to_string :: (n: f64) -> StringBuilder {
    str := string_builder.make_with_capacity(39 * 2 + 1);

    new_len := _write_f64_to_buf(n, (^mut u8).((^mut any).(str.buf)), 3);

    str.len = new_len;

    string_builder.shrink_to_fit(^mut str);

    str
}

_reverse_buf :: (buf: ^mut u8, start: usize, len: usize) {
    i := start;
    j := len - 1;
    temp := 0;

    while i < j {
        temp = ptr.read(buf, i);
        ptr.write(buf, ptr.read(buf, j), i);
        ptr.write(buf, temp, j);
        i = i + 1;
        j = j - 1;
    }
}

_write_i64_to_buf :: (n: i64, signed: bool, buf: ^mut u8, buf_offset: usize, required_digits: usize) -> usize {
    idx := buf_offset;

    n := n;
    is_neg := signed && n < 0;
    
    if is_neg {
        n = -n;
    }

    if n == 0 {
        ptr.write(buf, u8.('0'), idx);
        idx = idx + 1;
    }

    while n != 0 {
        remainder := n % 10;
        n = n / 10;

        ptr.write(buf, u8.('0') + u8.(remainder), idx);

        idx = idx + 1;
    }

    while idx - buf_offset < required_digits {
        ptr.write(buf, u8.('0'), idx);
        idx = idx + 1;
    }

    if is_neg {
        ptr.write(buf, u8.('-'), idx);
        idx = idx + 1;
    }

    _reverse_buf(buf, buf_offset, idx);

    idx
}

_write_f64_to_buf :: (n: f64, buf: ^mut u8, afterpoint: usize) -> usize {
    if math.is_NaN(n) {
        ptr.write(buf, u8.('N'), 0);
        ptr.write(buf, u8.('a'), 1);
        ptr.write(buf, u8.('N'), 2);
        return 3;
    } else if n == math.positive_inf {
        ptr.write(buf, u8.('+'), 0);
        ptr.write(buf, u8.('I'), 1);
        ptr.write(buf, u8.('n'), 2);
        ptr.write(buf, u8.('f'), 3);
        return 4;
    } else if n == math.negative_inf {
        ptr.write(buf, u8.('-'), 0);
        ptr.write(buf, u8.('I'), 1);
        ptr.write(buf, u8.('n'), 2);
        ptr.write(buf, u8.('f'), 3);
        return 4;
    }

    ipart := i64.(n);

    fpart := n - f64.(ipart);

    idx := _write_i64_to_buf(ipart, true, buf, 0, 0);

    if afterpoint != 0 {
        ptr.write(buf, u8.('.'), idx);

        fpart = fpart * f64.(math.pow(10, i32.(afterpoint)));

        // add one for the period
        idx = _write_i64_to_buf(i64.(fpart), false, buf, idx + 1, afterpoint);
    }

    idx
}
