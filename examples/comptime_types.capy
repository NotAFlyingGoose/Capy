
core :: mod "core";
ptr :: core.ptr;
libc :: core.libc;
meta :: core.meta;

io :: import "io.capy";

My_Type :: comptime {
    libc.puts("This is getting JIT'ed!");

    Field_Ty :: comptime {
        libc.puts("computing field type");

        input := io.get_user_input();

        if input == 32 {
            i32
        } else if input == 64 {
            i64
        } else {
            u8
        }
    };

    size :: comptime {
        6 / 2
    };

    struct {
        a: Field_Ty,
        b: [size] i32,
    }
};

main :: () {
    core.println("size: ", meta.size_of(My_Type));

    foo : My_Type = My_Type.{
        a = 42,
        b = i32.[4, 8, 15]
    };
    
    core.println(foo);
}

